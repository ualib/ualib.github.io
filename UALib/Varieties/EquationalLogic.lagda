---
layout: default
title : Varieties.EquationalLogic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="model-theory-and-equational-logic-types">Model Theory and Equational Logic</a>

This section presents the [Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][] where the binary "models" relation âŠ§, relating algebras (or classes of algebras) to the identities that they satisfy, is defined.

Agda supports the definition of infix operations and relations, and we use this to define âŠ§ so that we may write, e.g., `ğ‘¨ âŠ§ p â‰ˆ q` or `ğ’¦ âŠ§ p â‰‹ q`.

We also prove some closure and invariance properties of âŠ§.  In particular, we prove the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). The âŠ§ relation is an *algebraic invariant* (stable under isomorphism).

* [Subalgebraic invariance](#subalgebraic-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class.

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.



**Notation**. In the [Agda UALib][], because a class of structures has a different type than a single structure, we must use a slightly different syntax to avoid overloading the relations âŠ§ and â‰ˆ. As a reasonable alternative to what we would normally express informally as ğ’¦ âŠ§ ğ‘ â‰ˆ ğ‘, we have settled on ğ’¦ âŠ§ p â‰‹ q to denote this relation.  To reiterate, if ğ’¦ is a class of ğ‘†-algebras, we write ğ’¦ âŠ§ ğ‘ â‰‹ ğ‘ if every ğ‘¨ âˆˆ ğ’¦ satisfies ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘.

**Unicode Hints**. To produce the symbols â‰ˆ, âŠ§, and â‰‹ in [agda2-mode][], type `\~~`, `\models`, and `\~~~`, respectively.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Varieties.EquationalLogic where

open import Subalgebras.Subalgebras public
open import MGS-Embeddings using (embeddings-are-lc) public


module equational-logic {ğ‘† : Signature ğ“ ğ“¥}{ğ“§ : Level}{X : Set ğ“§} where
 open subalgebras {ğ‘† = ğ‘†} public


\end{code}


#### <a id="the-models-relation">The models relation</a>

We define the binary "models" relation âŠ§ using infix syntax so that we may write, e.g., `ğ‘¨ âŠ§ p â‰ˆ q` or `ğ’¦ âŠ§ p â‰‹ q`, relating algebras (or classes of algebras) to the identities that they satisfy. We also prove a coupld of useful facts about âŠ§.  More will be proved about âŠ§ in the next module, [Varieties.EquationalLogic](Varieties.EquationalLogic.html).

\begin{code}

 _âŠ§_â‰ˆ_ : Algebra ğ“¤ ğ‘† â†’ Term X â†’ Term X â†’ Set (ğ“§ âŠ” ğ“¤)
 ğ‘¨ âŠ§ p â‰ˆ q = âˆ€ a â†’ ğ‘¨ âŸ¦ p âŸ§ a â‰¡ ğ‘¨ âŸ¦ q âŸ§ a

 _âŠ§_â‰‹_ : Pred(Algebra ğ“¤ ğ‘†)(ov ğ“¤) â†’ Term X â†’ Term X â†’ Set (ğ“ âŠ” ğ“¥ âŠ” ğ“§ âŠ” lsuc ğ“¤)
 ğ’¦ âŠ§ p â‰‹ q = {ğ‘¨ : Algebra _ ğ‘†} â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ§ p â‰ˆ q

 \end{code}

 ##### <a id="semantics-of-âŠ§">Syntax and semantics of âŠ§</a>
 The expression `ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘` represents the assertion that the identity `p â‰ˆ q` holds when interpreted in the algebra ğ‘¨; syntactically, `ğ‘ Ì‡ ğ‘¨ â‰¡ ğ‘ Ì‡ ğ‘¨`.  It should be emphasized that the expression  `ğ‘ Ì‡ ğ‘¨ â‰¡ ğ‘ Ì‡ ğ‘¨` is interpreted computationally as an *extensional equality*, by which we mean that for each *assignment function*  `ğ’‚ :  X â†’ âˆ£ ğ‘¨ âˆ£`, assigning values in the domain of `ğ‘¨` to the variable symbols in `X`, we have `(ğ‘ Ì‡ ğ‘¨) ğ’‚ â‰¡ (ğ‘ Ì‡ ğ‘¨) ğ’‚`.




#### <a id="equational-theories-and-classes">Equational theories and models</a>

Here we define a type `Th` so that, if ğ’¦ denotes a class of algebras, then `Th ğ’¦` represents the set of identities modeled by all members of ğ’¦.

\begin{code}

 Th : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤) â†’ Pred(Term X Ã— Term X) (ğ“ âŠ” ğ“¥ âŠ” ğ“§ âŠ” lsuc ğ“¤)
 Th ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ§ p â‰‹ q

\end{code}

If â„° denotes a set of identities, then the class of algebras satisfying all identities in â„° is represented by `Mod â„°`, which we define in the following natural way.

\begin{code}

 Mod : Pred(Term X Ã— Term X)(ov ğ“¤) â†’ Pred(Algebra ğ“¤ ğ‘†) (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“§ âŠ” lsuc ğ“¤)
 Mod â„° = Î» ğ‘¨ â†’ âˆ€ p q â†’ (p , q) âˆˆ â„° â†’ ğ‘¨ âŠ§ p â‰ˆ q

\end{code}




#### <a id="algebraic-invariance-of-models">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}


 DFunExt : SetÏ‰
 DFunExt = (ğ“¤ ğ“¥ : Level) â†’ dfunext ğ“¤ ğ“¥

 âŠ§-I-invar : DFunExt â†’ {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†)(p q : Term X)
  â†’          ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ p â‰ˆ q

 âŠ§-I-invar {ğ“¤}{ğ“¦}fe{ğ‘¨} ğ‘© p q Apq (f , g , fâˆ¼g , gâˆ¼f) x =
   ğ‘© âŸ¦ p âŸ§ x                      â‰¡âŸ¨ refl âŸ©
   ğ‘© âŸ¦ p âŸ§ (âˆ£ ğ’¾ğ’¹ ğ‘© âˆ£ âˆ˜ x)         â‰¡âŸ¨ ap (Î» b â†’ ğ‘© âŸ¦ p âŸ§ b) (fe ğ“§ ğ“¦ Î» i â†’ (fâˆ¼g)(x i))â»Â¹ âŸ©
   ğ‘© âŸ¦ p âŸ§ ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x)  â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“¦) ğ‘© f p (âˆ£ g âˆ£ âˆ˜ x))â»Â¹ âŸ©
   âˆ£ f âˆ£ (ğ‘¨ âŸ¦ p âŸ§ (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ ap âˆ£ f âˆ£ (Apq (âˆ£ g âˆ£ âˆ˜ x)) âŸ©
   âˆ£ f âˆ£ (ğ‘¨ âŸ¦ q âŸ§ (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ comm-hom-term (fe ğ“¥ ğ“¦) ğ‘© f q (âˆ£ g âˆ£ âˆ˜ x) âŸ©
   ğ‘© âŸ¦ q âŸ§ ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x)  â‰¡âŸ¨ ap (Î» b â†’ ğ‘© âŸ¦ q âŸ§ b) (fe ğ“§ ğ“¦ Î» i â†’ (fâˆ¼g)(x i)) âŸ©
   ğ‘© âŸ¦ q âŸ§ x                      âˆ

\end{code}

As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that p Ì‡ ğ‘© â‰¡ q Ì‡ ğ‘© holds *extensionally*, that is, `âˆ€ x, (ğ‘© âŸ¦ p âŸ§) x â‰¡ (q Ì‡ ğ‘©) x`.

#### <a id="lift-invariance">Lift-invariance of âŠ§</a>
The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

 âŠ§-Lift-invar : DFunExt â†’ (p q : Term X){ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ ğ‘¨ âŠ§ p â‰ˆ q â†’ Lift-alg ğ‘¨ ğ“¦ âŠ§ p â‰ˆ q
 âŠ§-Lift-invar fe p q {ğ‘¨} Apq = âŠ§-I-invar fe (Lift-alg ğ‘¨ _) p q Apq Lift-â‰…

 âŠ§-lower-invar : DFunExt â†’ (p q : Term X){ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ Lift-alg ğ‘¨ ğ“¦ âŠ§ p â‰ˆ q â†’ ğ‘¨ âŠ§ p â‰ˆ q
 âŠ§-lower-invar fe p q {ğ‘¨} lApq = âŠ§-I-invar fe ğ‘¨ p q lApq (â‰…-sym Lift-â‰…)

\end{code}





#### <a id="subalgebraic-invariance">Subalgebraic invariance of âŠ§</a>

Identities modeled by an algebra ğ‘¨ are also modeled by every subalgebra of ğ‘¨, which fact can be formalized as follows.

\begin{code}

 âŠ§-S-invar : dfunext ğ“¥ ğ“¤ â†’ {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†){p q : Term X}
  â†’          ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘© â‰¤ ğ‘¨  â†’  ğ‘© âŠ§ p â‰ˆ q
 âŠ§-S-invar fe {ğ‘¨} ğ‘© {p}{q} Apq Bâ‰¤A b = (embeddings-are-lc âˆ£ h âˆ£ âˆ¥ Bâ‰¤A âˆ¥) (Î¾ b)
  where
  h : hom ğ‘© ğ‘¨
  h = âˆ£ Bâ‰¤A âˆ£

  Î¾ : âˆ€ b â†’ âˆ£ h âˆ£ (ğ‘© âŸ¦ p âŸ§ b) â‰¡ âˆ£ h âˆ£ (ğ‘© âŸ¦ q âŸ§ b)
  Î¾ b = âˆ£ h âˆ£(ğ‘© âŸ¦ p âŸ§ b)   â‰¡âŸ¨ comm-hom-term fe ğ‘¨ h p b âŸ©
        ğ‘¨ âŸ¦ p âŸ§ (âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ Apq (Î» x â†’ âˆ£ h âˆ£ (b x)) âŸ©
        ğ‘¨ âŸ¦ q âŸ§ (âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ (comm-hom-term fe ğ‘¨ h q b)â»Â¹ âŸ©
        âˆ£ h âˆ£(ğ‘© âŸ¦ q âŸ§ b)   âˆ

\end{code}

Next, identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

\begin{code}

 âŠ§-S-class-invar : dfunext ğ“¥ ğ“¤ â†’ {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}(p q : Term X)
  â†’                ğ’¦ âŠ§ p â‰‹ q â†’ (ğ‘© : SubalgebraOfClass{ğ“¤}{ğ“¦} ğ’¦) â†’ âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q
 âŠ§-S-class-invar fe p q Kpq (ğ‘© , ğ‘¨ , SA , (ka , BisSA)) = âŠ§-S-invar fe ğ‘© {p}{q}((Kpq ka)) (h , hem)
   where
   h : hom ğ‘© ğ‘¨
   h = âˆ˜-hom ğ‘© ğ‘¨ (âˆ£ BisSA âˆ£) âˆ£ snd SA âˆ£
   hem : is-embedding âˆ£ h âˆ£
   hem = âˆ˜-embedding (âˆ¥ snd SA âˆ¥) (isoâ†’embedding BisSA)
\end{code}





#### <a id="product-invariance">Product invariance of âŠ§</a>

An identity satisfied by all algebras in an indexed collection is also satisfied by the product of algebras in that collection.

\begin{code}

 module _ (I : Set ğ“¦)(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†) where

  âŠ§-P-invar : DFunExt â†’ {p q : Term X} â†’ (âˆ€ i â†’ ğ’œ i âŠ§ p â‰ˆ q) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q
  âŠ§-P-invar fe {p}{q} ğ’œpq a = Î³
   where
    Î³ : (â¨… ğ’œ)âŸ¦ p âŸ§ a  â‰¡  (â¨… ğ’œ )âŸ¦ q âŸ§ a
    Î³ = (â¨… ğ’œ)âŸ¦ p âŸ§ a      â‰¡âŸ¨ interp-prod (fe ğ“¥ (ğ“¤ âŠ” ğ“¦)) p ğ’œ a âŸ©
        (Î» i â†’ (ğ’œ i)âŸ¦ p âŸ§(Î» x â†’ (a x)i)) â‰¡âŸ¨ (fe ğ“¦ ğ“¤) (Î» i â†’ ğ’œpq i (Î» x â†’ a x i)) âŸ©
        (Î» i â†’ (ğ’œ i)âŸ¦ q âŸ§(Î» x â†’ (a x)i)) â‰¡âŸ¨ (interp-prod (fe ğ“¥ (ğ“¤ âŠ” ğ“¦)) q ğ’œ a)â»Â¹ âŸ©
        (â¨… ğ’œ)âŸ¦ q âŸ§ a                     âˆ

\end{code}

An identity satisfied by all algebras in a class is also satisfied by the product of algebras in the class.

\begin{code}

  âŠ§-P-class-invar : DFunExt â†’ {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}{p q : Term X}
   â†’                ğ’¦ âŠ§ p â‰‹ q â†’ (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q

  âŠ§-P-class-invar fe {ğ’¦}{p}{q}Î± Kğ’œ = âŠ§-P-invar fe {p}{q}Î» i â†’ Î± (Kğ’œ i)

\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models p â‰ˆ q if the lift of each algebra in the collection models p â‰ˆ q.

\begin{code}

  âŠ§-P-lift-invar : DFunExt â†’ {p q : Term X}
   â†’               (âˆ€ i â†’ (Lift-alg (ğ’œ i) ğ“¦) âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q

  âŠ§-P-lift-invar fe {p}{q} Î± = âŠ§-P-invar fe {p}{q}Aipq
    where
     Aipq : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
     Aipq i = âŠ§-lower-invar fe p q (Î± i) --  (â‰…-sym Lift-â‰…)

\end{code}


#### <a id="homomorphisc-invariance">Homomorphic invariance of âŠ§</a>

If an algebra ğ‘¨ models an identity p â‰ˆ q, then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

\begin{code}

 âŠ§-H-invar : DFunExt â†’ {p q : Term X}{ğ‘¨ : Algebra ğ“¤ ğ‘†}(Ï† : hom (ğ‘» X) ğ‘¨)
  â†’          ğ‘¨ âŠ§ p â‰ˆ q â†’ âˆ£ Ï† âˆ£ p â‰¡ âˆ£ Ï† âˆ£ q

 âŠ§-H-invar {ğ“¤} fe {p}{q}{ğ‘¨} Ï† Î² = âˆ£ Ï† âˆ£ p      â‰¡âŸ¨ ap âˆ£ Ï† âˆ£ (term-agreement (fe ğ“¥ (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“§)) p) âŸ©
                 âˆ£ Ï† âˆ£ ((ğ‘» X)âŸ¦ p âŸ§ â„Š)   â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ Ï† p â„Š ) âŸ©
                 ğ‘¨ âŸ¦ p âŸ§ (âˆ£ Ï† âˆ£ âˆ˜ â„Š)  â‰¡âŸ¨ Î² (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) âŸ©
                 ğ‘¨ âŸ¦ q âŸ§ (âˆ£ Ï† âˆ£ âˆ˜ â„Š)  â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ Ï† q â„Š )â»Â¹ âŸ©
                 âˆ£ Ï† âˆ£ ((ğ‘» X)âŸ¦ q âŸ§ â„Š)  â‰¡âŸ¨(ap âˆ£ Ï† âˆ£ (term-agreement (fe ğ“¥ (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“§)) q))â»Â¹ âŸ©
                 âˆ£ Ï† âˆ£ q                âˆ

\end{code}

More generally, an identity is satisfied by all algebras in a class if and only if that identity is invariant under all homomorphisms from the term algebra `ğ‘» X` into algebras of the class. More precisely, if `ğ’¦` is a class of `ğ‘†`-algebras and `p`, `q` terms in the language of `ğ‘†`, then,

```agda
  ğ’¦ âŠ§ p â‰ˆ q  â‡”  âˆ€ ğ‘¨ âˆˆ ğ’¦,  âˆ€ Ï† : hom (ğ‘» X) ğ‘¨,  Ï† âˆ˜ (ğ‘» X)âŸ¦ p âŸ§ â‰¡ Ï† âˆ˜ (ğ‘» X)âŸ¦ q âŸ§
```




-------------------------------------

[â†‘ Varieties](Varieties.html)
<span style="float:right;">[Varieties.Varieties â†’](Varieties.Varieties.html)</span>

{% include UALib.Links.md %}



<!--

 module _ {ğ“¤ : Level}{ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}  where

  -- â‡’ (the "only if" direction)
  âŠ§-H-class-invar : DFunExt â†’ {p q : Term X} â†’ ğ’¦ âŠ§ p â‰‹ q â†’ âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ (a : X â†’ Term X)
   â†’                (âˆ£ Ï† âˆ£ (_âŸ¦_âŸ§_ {ğ“¤ = (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤)} (ğ‘» X) p a)) â‰¡ (âˆ£ Ï† âˆ£ (_âŸ¦_âŸ§_ {ğ“¤ = (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤)} (ğ‘» X) q a))
  âŠ§-H-class-invar fe {p}{q} Î± ğ‘¨ Ï† ka a = {!!}
   -- where
   --  Î³ : (âˆ£ Ï† âˆ£((ğ‘» X)âŸ¦ p âŸ§ a)) â‰¡ (âˆ£ Ï† âˆ£((ğ‘» X)âŸ¦ q âŸ§ a))
   --  Î³ = âˆ£ Ï† âˆ£ (ğ‘» X)âŸ¦ p âŸ§ a  â‰¡âŸ¨ comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ Ï† p a âŸ©
   --        ğ‘¨ âŸ¦ p âŸ§ (âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ happly (Î± ka) (âˆ£ Ï† âˆ£ âˆ˜ a) âŸ©
   --        ğ‘¨ âŸ¦ q âŸ§ (âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ Ï† q a)â»Â¹ âŸ©
   --        âˆ£ Ï† âˆ£ (ğ‘» X)âŸ¦ q âŸ§ a  âˆ


 -- â‡ (the "if" direction)
  âŠ§-H-class-coinvar : DFunExt â†’ {p q : Term X}
   â†’  (âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£ (ğ‘» X)âŸ¦ p âŸ§ a â‰¡ âˆ£ Ï† âˆ£(ğ‘» X)âŸ¦ q âŸ§ a) â†’ ğ’¦ âŠ§ p â‰‹ q

  âŠ§-H-class-coinvar fe {p}{q} Î² {ğ‘¨} ka = {!!}
   where
   Ï† : (ğ’‚ : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
   Ï† ğ’‚ = lift-hom ğ‘¨ ğ’‚

   Î³ : ğ‘¨ âŠ§ p â‰ˆ q
   Î³ = {!!} -- (fe (ğ“§ âŠ” ğ“¤) ğ“¤) Î» ğ’‚ â†’ ğ‘¨ âŸ¦ p âŸ§(âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ â„Š)     â‰¡âŸ¨(comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ (Ï† ğ’‚) p â„Š)â»Â¹ âŸ©
         --        âˆ£ Ï† ğ’‚ âˆ£ (ğ‘» X) âŸ¦ p âŸ§ â„Š  â‰¡âŸ¨ cong-app (Î² ğ‘¨ (Ï† ğ’‚) ka) â„Š âŸ©
         --        âˆ£ Ï† ğ’‚ âˆ£ (ğ‘» X) âŸ¦ q âŸ§ â„Š  â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“¤) ğ‘¨ (Ï† ğ’‚) q â„Š) âŸ©
         --        ğ‘¨ âŸ¦ q âŸ§ (âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ â„Š)     âˆ


  âŠ§-H : DFunExt â†’ {p q : Term X} â†’ ğ’¦ âŠ§ p â‰‹ q â‡” (âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£(ğ‘» X)âŸ¦ p âŸ§ a â‰¡ âˆ£ Ï† âˆ£(ğ‘» X)âŸ¦ q âŸ§ a)
  âŠ§-H fe {p}{q} = âŠ§-H-class-invar fe {p}{q} , âŠ§-H-class-coinvar fe {p}{q} 

-->

